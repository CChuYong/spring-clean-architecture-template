//plugins {
//	id 'java'
//	id 'org.springframework.boot' version '2.7.5' apply false
//	id 'io.spring.dependency-management' version '1.0.15.RELEASE' apply false
//}

buildscript {
	ext {
		springBootVersion = '2.7.5'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
		classpath "io.spring.gradle:dependency-management-plugin:1.0.15.RELEASE"
	}
}

subprojects {
	apply plugin: 'java-library'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	group = 'kr.chuyong'
	version = '0.0.1-SNAPSHOT'
	sourceCompatibility = 17

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	repositories {
		mavenCentral()
	}

	dependencies {
		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'

		testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
		testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
	}

	bootJar { enabled = false }
	jar { enabled = true }

	test {
		useJUnitPlatform()
	}
}

project(':infrastructure') {
	dependencies {
		implementation project(':entity')
		implementation project(':use-case')

		api 'org.springframework.boot:spring-boot-starter-data-jpa'
		runtimeOnly 'com.mysql:mysql-connector-j'
	}
}

project(':presenter') {
	dependencies {
		implementation project(':entity')
		implementation project(':use-case')

		api 'org.springframework.boot:spring-boot-starter-web'
	}
}

project(':config') {
	apply plugin: 'java-library'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	bootJar { enabled = true }
	jar { enabled = true }

	dependencies {
		api project(':entity')
		api project(':use-case')
		api project(':presenter')
		api project(':infrastructure')

		api 'org.springframework.boot:spring-boot-starter-web'
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
	}
}
